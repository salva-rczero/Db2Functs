KB5AM11  CEEENTRY AUTO=PROGSIZE,MAIN=YES,PLIST=OS
         YREGS
V1       EQU   1
V2       EQU   2
V3       EQU   3
V4       EQU   4
         USING PROGAREA,R13
         LR    R10,R1              Get pointer to parm
* Load Parm
         USING PARM,R10
         L     R6,PTRIP            Get pointer to entrada
         L     R3,PTRRES
         L     R4,PTRINDI
         L     R5,PTRINDR
         DROP  R10
* Check String
         LH    R1,0(R4)            Load String indicator
         LTR   R1,R1               Check it it is negative
         JM    NULL                If so, result is NULL
* Load Parm
         MVC   CTA,ZEROSP      Init cta
         LA    R2,CTA+16
         LH    R1,0(R6)        get length
         SR    R2,R1
         BCTR  R1,0
         EX    R1,MOVCTA
         NC    CTA,=XL16'0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F0F'
* Main
         VL    V1,CTA          Load vectors: v1=cta
         VLM   V2,V3,COEF      Load vectors: v2=coef,v3=zero
         VMLB  V4,V1,V2        v4=v1*v2
         VSUMB V1,V4,V3        v1=v4(b1+b2+b3+b4)(b5+b6+b7+b8)...
         VSUMGF V4,V1,V3       v4=v1(f1+f2)(f3+f4)
         VSUMQG V1,V4,V3       v1=v4(g1+g2)
         VSTEG V1,SUM,1        store 2nd double
         LG    R1,SUM          r1=sum
         LLILL R2,11           r2=11
         DSGFR R0,R2           divide r0||r1/11
         LLILL R1,11           r1=11
         SR    R1,R0           r1=11-resto
         STCM  R1,B'0001',0(R3) save
         TR    0(1,R3),=XL12'F0F1F2F3F4F5F6F7F8F9F1F0'
         MVC   0(2,R5),=H'0'       Not null
         J     END
NULL     MVC   0(2,R5),=H'-1'      NULL result
         J     END
ERROR    MVC   0(2,R5),=H'-1'      NULL result
         J     END
END      CEETERM  RC=0
*******************************************************************
*  VARIABLE DECLARATIONS AND EQUATES                              *
*******************************************************************
PPA      CEEPPA  ,                 CONSTANTS DESCRIBING THE CODE BLOCK
         LTORG ,                   PLACE LITERAL POOL HERE
COEF     DC    XL16'00000408050A01020408050A09070306'
ZEROS    DC    XL16'00000000000000000000000000000000'
ZEROSP   DC    CL16'0000000000000000'
MOVCTA   MVC   0(0,R2),2(R6)
PROGAREA DSECT
         ORG   *+CEEDSASZ          LEAVE SPACE FOR DSA FIXED PART
         DS    0D
CTA      DS    CL16
SUM      DS    D
PROGSIZE EQU   *-PROGAREA
         CEEDSA  ,                 MAPPING OF THE DYNAMIC SAVE AREA
         CEECAA  ,                 MAPPING OF THE COMMON ANCHOR AREA
EZATASK  EZASMI TYPE=TASK,STORAGE=DSECT
         IHAPSA DSECT=YES,LIST=NO
PARM     DSECT
PTRIP    DS    A                   Pointer to String
PTRRES   DS    A                   Pointer to Result
PTRINDI  DS    A                   Pointer to String   indicator
PTRINDR  DS    A                   Pointer to Result indicator
         END   KB5AM11
